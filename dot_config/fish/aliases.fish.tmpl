# Command replacements (transparent - use alias)
alias ls='eza'
alias ll='eza -l --icons --git'
alias lt='eza --tree --icons --level=2'
alias cat='bat'
alias catp='bat --plain'
alias vim='nvim'

# Git extras (plugin-git provides: gst, gco, gcm, gp, gl, grbi, gsta, etc.)
abbr -a gcmsg 'git commit -m'
abbr -a gundo 'git reset HEAD~'
abbr -a lg 'lazygit'
abbr -a grecent 'git recent'
abbr -a gcleanup 'git cleanup'
abbr -a gabsorb 'git absorb'

# Docker (V2 syntax)
abbr -a dco 'docker compose'
abbr -a dce 'docker compose exec'
abbr -a dl 'docker ps -l -q'

# Zoxide
abbr -a zq 'zoxide query'
abbr -a zqi 'zoxide query -i'
abbr -a zr 'zoxide remove'
abbr -a za 'zoxide add'

# Chezmoi
abbr -a dotfiles 'cd (chezmoi source-path)'
abbr -a dotfiles-work 'cd ~/.local/share/chezmoi-work'

# fd (find replacement)
abbr -a ff 'fd --type f'
abbr -a fdir 'fd --type d'

# ripgrep
abbr -a rg 'rg --smart-case'

# Graphite (only if installed)
if command -q gt
    # Navigation
    abbr -a gtco 'gt checkout'
    abbr -a gtg 'gt get'
    abbr -a gtk 'gt checkout trunk'

    # Creating/committing
    abbr -a gtc 'gt create -am'
    abbr -a gtcn 'gt create --no-verify'
    abbr -a gtcm 'gt commit -m'
    abbr -a gtmsg 'gt commit -m'

    # Submitting PRs
    abbr -a gts 'gt submit --stack --update-only'
    abbr -a gtss 'gt submit --stack'

    # Viewing stack
    abbr -a gtl 'gt log short'
    abbr -a gtll 'gt log long'
    abbr -a gtlo 'gt log'
    abbr -a gtls 'gt log short -s'
    abbr -a gtlc 'gt log --classic'
end

# procs (ps replacement)
alias ps='procs'
alias pst='procs --tree'

# bottom (htop replacement)
alias top='btm'

# Functions (for complex commands with pipes/subshells)
function reload! --description "Reload fish config"
    source ~/.config/fish/config.fish
end

function gapan --description "Git add intent-to-add + patch"
    git add --intent-to-add . && git add --patch
end

function gcob --description "Git checkout branch with fzf"
    set -l branch (git branch -a --color=always | grep -v "/HEAD\s" | fzf --ansi | sed "s/.*\///" | sed "s/^[* ]*//")
    if test -n "$branch"
        git checkout $branch
    end
end

function cb --description "Copy to clipboard (cross-platform)"
    if command -q pbcopy
        # macOS
        pbcopy
    else if command -q xclip
        # Linux
        xclip -selection clipboard
    else
        echo "Error: No clipboard command found (pbcopy or xclip)" >&2
        return 1
    end
end


function gstash --description "Browse and apply git stashes with fzf"
    if test (git stash list | count) -eq 0
        echo "No stashes found"
        return 1
    end

    set -l stash (git stash list --color=always | fzf \
        --ansi \
        --header "Enter=apply, Ctrl-d=drop, Ctrl-p=pop" \
        --preview "git stash show -p --color=always {1} 2>/dev/null" \
        --preview-window "right:60%" \
        --bind "ctrl-d:execute(git stash drop {1})+reload(git stash list --color=always)" \
        --bind "ctrl-p:execute(git stash pop {1})+abort" \
        | cut -d: -f1)

    if test -n "$stash"
        git stash apply "$stash"
    end
end

function gdiff --description "Browse changed files with diff preview"
    set -l files (git diff --name-only HEAD 2>/dev/null)

    if test (count $files) -eq 0
        echo "No changed files"
        return 1
    end

    set -l file (printf '%s\n' $files | fzf \
        --header "Changed files (Enter=open in nvim)" \
        --preview "git diff --color=always HEAD -- {} 2>/dev/null" \
        --preview-window "right:60%")

    if test -n "$file"
        nvim "$file"
    end
end

{{ if eq .machine "workstation" -}}
# World monorepo shortcuts (this machine only)
set -gx WORLD_ROOT ~/world
set -gx WORLD_TREES ~/world/trees
set -gx WORLD_SRC ~/world/trees/root/src

abbr -a wcd 'cd ~/world/trees/root/src'
abbr -a wls 'ls ~/world/trees/root/src'
abbr -a wtrees 'ls ~/world/trees'
abbr -a wtcd 'cd ~/world/trees'

function wt --description "cd into a World worktree by name"
    if test (count $argv) -eq 0
        ls ~/world/trees
        return
    end
    set -l tree ~/world/trees/$argv[1]
    if test -d "$tree"
        cd "$tree"
    else
        echo "Worktree '$argv[1]' not found"
        return 1
    end
end

function wt-fzf --description "Pick a World worktree with fzf"
    set -l tree (ls ~/world/trees | fzf --header "Select worktree")
    if test -n "$tree"
        cd ~/world/trees/$tree
    end
end
{{ end -}}

